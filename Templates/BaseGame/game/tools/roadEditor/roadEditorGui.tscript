//-----------------------------------------------------------------------------
// Copyright (c) 2012 GarageGames, LLC
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to
// deal in the Software without restriction, including without limitation the
// rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
// sell copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
// FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
// IN THE SOFTWARE.
//-----------------------------------------------------------------------------

function RoadEditorGui::onWake( %this )
{   
   $DecalRoad::EditorOpen = true;      
   
   %fixedWindow = RoadEditorTreeWindow;
   %fluidWindow = RoadEditorOptionsWindow;
   
   if(EditorSettings.value( "WorldEditor/forceSidebarToSide" ) == 1)
   {
      // Let's dock the side panel to the right side
      %this.docked = false;
      %this.resizing = true;
      %this.dockSidePanel();
   }
   else
   {
      // Let's release the side panel so it can be moved
      %this.docked = true;
      %this.resizing = false;
      %this.releaseSidePanel();
   }
   
   %count = EWorldEditor.getSelectionSize();
   for ( %i = 0; %i < %count; %i++ )
   {
      %obj = EWorldEditor.getSelectedObject(%i);
      if ( %obj.getClassName() !$= "DecalRoad" )
         EWorldEditor.unselectObject();
      else
         %this.setSelectedRoad( %obj );
   }        
   
   %this.onNodeSelected(-1);
}

function RoadEditorGui::onSleep( %this )
{
   $DecalRoad::EditorOpen = false;   
}

//-----------------------------------------------------------------------------

function RoadEditorGui::maxSize(%this, %window)
{      
   // Resize the windows to the max height
   // and force these to the right side if set
   if(EditorSettings.value( "WorldEditor/forceSidebarToSide" ) == 1 && %this.resizing == true)
   {  
      // prevent onResize after a resize
      %this.resizing = false;
      
      %fixedWindow = RoadEditorTreeWindow;
      %fluidWindow = RoadEditorOptionsWindow;
      %top = EditorGuiToolbar.extent.y + 6;
      %bottom = %top + 65 - 6;
      %maxHeight = Canvas.extent.y - %top - %bottom;
      
      // --- Fixed window (top) ------------------------------------------------
      // put it back if it moved
      %fixedWindow.position.x = Canvas.extent.x - %fixedWindow.extent.x;
      %fixedWindow.position.y = %top;
      
      // don't go beyond the canvas
      if(%fixedWindow.extent.y > %maxHeight)
         %fixedWindow.extent.y = %maxHeight - %fluidWindow.extent.y;

      %position = %fixedWindow.position.x SPC %fixedWindow.position.y;
      %extent = %window.extent.x SPC %fixedWindow.extent.y;
      %fixedWindow.resize(%position.x, %position.y, %extent.x, %extent.y);
      
      // --- Fluid window (bottom) ---------------------------------------------
      // position is relative to the top window
      %position = %fixedWindow.position.x SPC %fixedWindow.extent.y + %top;
      %extent = %window.extent.x SPC Canvas.extent.y - %fixedWindow.extent.y - %bottom;
      %fluidWindow.resize(%position.x, %position.y, %extent.x, %extent.y);
      
      // --- AssetBrowser window  ----------------------------------------------
      if(isObject(AssetBrowserWindow))
      {
         // Only resize the AssetBrowser if it's docked
         if(AssetBrowserWindow.docked == true)
         {
            // The width is relative to the sidepanel
            %browserWidth = Canvas.extent.x - %extent.x;
            %browserHeight = AssetBrowserWindow.extent.y;
            %browserPosY = Canvas.extent.y - AssetBrowserWindow.extent.y - 33;
            AssetBrowserWindow.resize(0, %browserPosY, %browserWidth, %browserHeight);
         }
      }
      // --- Windowed Console --------------------------------------------------
      if(isObject(windowConsoleControl))
      {
         // Only resize the AssetBrowser if it's docked
         if(windowConsoleControl.docked == true)
         {
            // The width is relative to the sidepanel
            %consoleWidth = Canvas.extent.x - %extent.x;
            %consoleHeight = windowConsoleControl.extent.y;
            %consolePosY = Canvas.extent.y - windowConsoleControl.extent.y - 33;
            windowConsoleControl.resize(0, %consolePosY, %consoleWidth, %consoleHeight);
         }
      }
   }
}

function RoadEditorTreeWindow::onMouseDragged(%this)
{
   %parent = RoadEditorGui;
   
   if(%parent.panelHidden == true)
   {
      %parent.showSidePanel();
   }
   
   if(%parent.resizing == false && %parent.docked == true)
   {
      %parent.resizing = true;
      %parent.maxSize(%this);
   }
}

function RoadEditorOptionsWindow::onMouseDragged(%this)
{
   %parent = RoadEditorGui;
   
   if(%parent.panelHidden == true)
   {
      %parent.showSidePanel();
   }
   
   if(%parent.resizing == false && %parent.docked == true)
   {
      %parent.resizing = true;
      %parent.maxSize(%this);
   }
}

function RoadEditorGui::onResize(%this, %newPosition, %newExtent)
{
   // Window to focus on (mostly the fluid window)
   %window = RoadEditorOptionsWindow;
   
   if(%window.panelHidden == true)
   {
      %window.showSidePanel();
   }
   
   if(%this.resizing == false && %this.docked == true)
   {  
      // Only resize once
      %this.resizing = true;
      %this.maxSize(%window);
   }
}

function RoadEditorGui::dockSidePanel()
{
   %parent = RoadEditorGui;
   %fixedWindow = RoadEditorTreeWindow;
   %fluidWindow = RoadEditorOptionsWindow;
   
   if(%parent.docked == true)
      return;
   
   // Move and resize the window(s)
   %parent.resizing = true;
   %parent.maxSize(%fluidWindow);
      
   %parent.docked = true;
   %fluidWindow.onMouseDragged();

   // Lock the windows in place
   %fixedWindow.canCollapse = "0";
   %fixedWindow.canMove = "0";
   
   %fluidWindow.canCollapse = "0";
   %fluidWindow.canMove = "0";
   
   RoadEditorGui_UnDockBtn.Visible = "1";
   RoadEditorGui_DockBtn.Visible = "0";
   
   RoadEditorGui_showBtn.Visible = "0";
   RoadEditorGui_hideBtn.Visible = "1";
}

function RoadEditorGui::releaseSidePanel()
{      
   %parent = RoadEditorGui;
   %fixedWindow = RoadEditorTreeWindow;
   %fluidWindow = RoadEditorOptionsWindow;
   
   if(%parent.docked == false)
      return;
   
   // Unlock the windows so that be moved
   %fixedWindow.canCollapse = "1";
   %fixedWindow.canMove = "1";
   
   %fluidWindow.canCollapse = "1";
   %fluidWindow.canMove = "1";
   
   RoadEditorGui_UnDockBtn.Visible = "0";
   RoadEditorGui_DockBtn.Visible = "1";
   
   RoadEditorGui_showBtn.Visible = "0";
   RoadEditorGui_hideBtn.Visible = "0";
   
   // Let's do a small resize so it's visually clear we're undocking
   %position = %fixedWindow.position.x - 6 SPC %fixedWindow.position.y + 6;
   %extent = %fixedWindow.extent.x SPC %fixedWindow.extent.y;
   %fixedWindow.resize(%position.x, %position.y, %extent.x, %extent.y);

   %position =  %fluidWindow.position.x - 6 SPC  %fluidWindow.position.y + 6;
   %extent = %fluidWindow.extent.x SPC %fluidWindow.extent.y - 12;
   %fluidWindow.resize(%position.x, %position.y, %extent.x, %extent.y);
   
   %parent.docked = false;
   %parent.resizing = false;
}

function RoadEditorGui::hideSidePanel()
{
   %parent = RoadEditorGui;
   %fixedWindow = RoadEditorTreeWindow;
   %fluidWindow = RoadEditorOptionsWindow;
   
   RoadEditorGui_showBtn.Visible = "1";
   RoadEditorGui_hideBtn.Visible = "0";
   
   // hide the content of the panels
   %fixedWindow.titleText = %fixedWindow.text;
   %fluidWindow.titleText = %fluidWindow.text;
   
   %fixedWindow.text = "";
   RoadEditorTreeWPanel.Visible = "0";
   
   %fluidWindow.text = "";
   RoadEditorProperties.Visible = "0";
   RoadEditorPropPanel.Visible = "0";
   RoadEditorPropScroll.Visible = "0";
   
   // Let's do a resize so that the panel is collapsed to the side
   %position = Canvas.extent.x - 24 SPC %fixedWindow.position.y;
   %extent = %fixedWindow.extent.x SPC %fixedWindow.extent.y;
   %fixedWindow.resize(%position.x, %position.y, %extent.x, %extent.y);

   %position =  Canvas.extent.x - 24 SPC  %fluidWindow.position.y;
   %extent = %fluidWindow.extent.x SPC %fluidWindow.extent.y;
   %fluidWindow.resize(%position.x, %position.y, %extent.x, %extent.y);
   
   %parent.panelHidden = true;
}

function RoadEditorGui::showSidePanel()
{
   %parent = RoadEditorGui;
   %fixedWindow = RoadEditorTreeWindow;
   %fluidWindow = RoadEditorOptionsWindow;
   
   RoadEditorGui_showBtn.Visible = "0";
   RoadEditorGui_hideBtn.Visible = "1";
   
   // show the content of the panels
   // hide the content of the panels
   %fixedWindow.text = %fixedWindow.titleText;
   RoadEditorTreeWPanel.Visible = "1";
   
   %fluidWindow.text = %fluidWindow.titleText;
   RoadEditorProperties.Visible = "1";
   RoadEditorPropPanel.Visible = "1";
   RoadEditorPropScroll.Visible = "1";
   
   %parent.resizing = true;
   %parent.maxSize(%fluidWindow);
   
   %parent.panelHidden = false;
}

//-----------------------------------------------------------------------------

function RoadEditorGui::paletteSync( %this, %mode )
{
   %evalShortcut = "ToolsPaletteArray-->" @ %mode @ ".setStateOn(1);";
   eval(%evalShortcut);
}  

function RoadEditorGui::onDeleteKey( %this )
{
   %road = %this.getSelectedRoad();
   %node = %this.getSelectedNode();
   
   if ( !isObject( %road ) )
      return;
      
   if ( %node != -1 )
   {
      %this.deleteNode();
   }
   else
   {
      toolsMessageBoxOKCancel( "Notice", "Delete selected DecalRoad?", "RoadEditorGui.deleteRoad();", "" );
   }   
}

function RoadEditorGui::onEscapePressed( %this )
{
   if( %this.getMode() $= "RoadEditorAddNodeMode" )
   {
      %this.prepSelectionMode();
      return true;
   }
   return false;
}

//just in case we need it later
function RoadEditorGui::onRoadCreation( %this )
{
   %this.road.MaterialAsset = "Core_GameObjects:DefaultDecalRoadMaterial";
}

function RoadEditorGui::onRoadSelected( %this, %road )
{      
   %this.road = %road;
   
   // Update the materialEditorList
   if(isObject( %road ))
      $Tools::materialEditorList = %road.getId();
   
   RoadInspector.inspect( %road );  
   RoadTreeView.buildVisibleTree(true);
   if( RoadTreeView.getSelectedObject() != %road )
   {
      RoadTreeView.clearSelection();
      %treeId = RoadTreeView.findItemByObjectId( %road );
      RoadTreeView.selectItem( %treeId );  
   }
}

function RoadEditorGui::onNodeSelected( %this, %nodeIdx )
{
   
   if ( %nodeIdx == -1 )
   {
      RoadEditorProperties-->position.setActive( false );
      RoadEditorProperties-->position.setValue( "" );    
      
      RoadEditorProperties-->width.setActive( false );
      RoadEditorProperties-->width.setValue( "" );  
   }
   else
   {
      RoadEditorProperties-->position.setActive( true );
      RoadEditorProperties-->position.setValue( %this.getNodePosition() );    
      
      RoadEditorProperties-->width.setActive( true );
      RoadEditorProperties-->width.setValue( %this.getNodeWidth() );  
   }
   
}

function RoadEditorGui::onNodeModified( %this, %nodeIdx )
{
   
   RoadEditorProperties-->position.setValue( %this.getNodePosition() );    
   RoadEditorProperties-->width.setValue( %this.getNodeWidth() );  

}

function RoadEditorGui::editNodeDetails( %this )
{
   
   %this.setNodePosition( RoadEditorProperties-->position.getText() );
   %this.setNodeWidth( RoadEditorProperties-->width.getText() );
}

function RoadEditorGui::onBrowseClicked( %this )
{
   %filename = RETextureFileCtrl.getText();
         
   %dlg = new OpenFileDialog()
   {
      Filters        = "All Files (*.*)|*.*|";
      DefaultPath    = RoadEditorGui.lastPath;
      DefaultFile    = %filename;
      ChangePath     = false;
      MustExist      = true;
   };
         
   %ret = %dlg.Execute();
   if(%ret)
   {
      RoadEditorGui.lastPath = filePath( %dlg.FileName );
      %filename = %dlg.FileName;
      RoadEditorGui.setTextureFile( %filename );
      RETextureFileCtrl.setText( %filename );
   }
   
   %dlg.delete();
}

function RoadInspector::inspect( %this, %obj )
{
   %name = "";
   if ( isObject( %obj ) )
      %name = %obj.getName();   
   else
      RoadFieldInfoControl.setText( "" );
   
   //RoadInspectorNameEdit.setValue( %name );
   Parent::inspect( %this, %obj );  
}

function RoadInspector::onInspectorFieldModified( %this, %object, %fieldName, %arrayIndex, %oldValue, %newValue )
{
   // Same work to do as for the regular WorldEditor Inspector.
   Inspector::onInspectorFieldModified( %this, %object, %fieldName, %arrayIndex, %oldValue, %newValue );   
}

function RoadInspector::onFieldSelected( %this, %fieldName, %fieldTypeStr, %fieldDoc )
{
   RoadFieldInfoControl.setText( "<font:" @ $Gui::fontTypeBold @ ":16>" @ %fieldName @ "<font:" @ $Gui::fontTypeItalic @ ":16> (" @ %fieldTypeStr @ ") " NL "<font:" @ $Gui::fontTypeRegular @ ":16>" @ %fieldDoc );
}

function RoadTreeView::onInspect(%this, %obj)
{
   RoadInspector.inspect(%obj);   
}

function RoadTreeView::onSelect(%this, %obj)
{
   RoadEditorGui.road = %obj; 
   RoadInspector.inspect( %obj );
   if(%obj != RoadEditorGui.getSelectedRoad())
   {
      RoadEditorGui.setSelectedRoad( %obj );
   }
}

function RoadEditorGui::prepSelectionMode( %this )
{
   %mode = %this.getMode();
   
   if ( %mode $= "RoadEditorAddNodeMode"  )
   {
      if ( isObject( %this.getSelectedRoad() ) )
         %this.deleteNode();
   }
   
   %this.setMode( "RoadEditorSelectMode" );
   ToolsPaletteArray-->RoadEditorSelectMode.setStateOn(1);
}

//------------------------------------------------------------------------------
function ESettingsWindow::getRoadEditorSettings(%this)
{
   SettingsInspector.startGroup("Defaults");
   SettingsInspector.addSettingsField("RoadEditor/DefaultWidth", "Width", "string", "");
   SettingsInspector.addSettingsField("RoadEditor/MaterialName", "Material", "string", "");
   SettingsInspector.endGroup();
   
   SettingsInspector.startGroup("Colors");
   SettingsInspector.addSettingsField("RoadEditor/HoverSplineColor", "Hover Spline", "colorI", "");
   SettingsInspector.addSettingsField("RoadEditor/SelectedSplineColor", "Selected Spline", "colorI", "");
   SettingsInspector.endGroup();
}
//------------------------------------------------------------------------------
function ERoadEditorSelectModeBtn::onClick(%this)
{
   EditorGuiStatusBar.setInfo(%this.ToolTip);
}

function ERoadEditorAddModeBtn::onClick(%this)
{
   EditorGuiStatusBar.setInfo(%this.ToolTip);
}

function ERoadEditorMoveModeBtn::onClick(%this)
{
   EditorGuiStatusBar.setInfo(%this.ToolTip);
}

function ERoadEditorScaleModeBtn::onClick(%this)
{
   EditorGuiStatusBar.setInfo(%this.ToolTip);
}

function ERoadEditorInsertModeBtn::onClick(%this)
{
   EditorGuiStatusBar.setInfo(%this.ToolTip);
}

function ERoadEditorRemoveModeBtn::onClick(%this)
{
   EditorGuiStatusBar.setInfo(%this.ToolTip);
}

function RoadDefaultWidthSliderCtrlContainer::onWake(%this)
{
   RoadDefaultWidthSliderCtrlContainer-->slider.setValue(RoadDefaultWidthTextEditContainer-->textEdit.getText());
}