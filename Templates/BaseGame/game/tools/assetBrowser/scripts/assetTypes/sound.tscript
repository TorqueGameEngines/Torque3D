function AssetBrowser::editSoundAsset(%this, %assetDef)
{
   if (isObject($PreviewSoundSource))
        sfxStop($PreviewSoundSource);
   $PreviewSoundSource = %assetDef.playSound();
}

function AssetBrowser::onSoundAssetChanged(%this, %assetDef)
{
   if (isObject($PreviewSoundSource))
        sfxStop($PreviewSoundSource);
}

function AssetBrowser::moveSoundAsset(%this, %assetDef, %destination)
{
   %currentModule = AssetDatabase.getAssetModule(%assetDef.getAssetId());
   %targetModule = AssetBrowser.dirHandler.getModuleFromAddress(%destination);
   
   %newAssetPath = moveAssetFile(%assetDef, %destination);
   
   if(%newAssetPath $= "")
      return false;

   moveAssetLooseFile(%assetDef.getSoundPath(), %destination);
   
   AssetDatabase.removeDeclaredAsset(%assetDef.getAssetId());
   AssetDatabase.addDeclaredAsset(%targetModule, %newAssetPath);
}

function AssetBrowser::buildSoundAssetPreview(%this, %assetDef, %previewData)
{
   %previewData.assetName = %assetDef.assetName;
   %previewData.assetPath = %assetDef.soundFilePath;
   
   if(%this.selectMode)
   {
      %previewData.doubleClickCommand = "AssetBrowser.selectAsset( AssetBrowser.selectedAsset );";
   }
   else
   {
      if(EditorSettings.value("Assets/Browser/doubleClickAction", "Edit Asset") $= "Edit Asset")
      {
      %previewData.doubleClickCommand = "AssetBrowser.editAsset( "@%assetDef@" );";
      }
      else
      {
         %previewData.doubleClickCommand = "AssetBrowser.onSoundAssetEditorDropped( "@%assetDef@" );";
      }
   }
   
   %previewData.previewImage = "ToolsModule:soundIcon_image";   
   
   %previewData.assetFriendlyName = %assetDef.assetName;
   %previewData.assetDesc = %assetDef.description;
   %previewData.tooltip = "Asset Name: " @ %assetDef.assetName @ "\nDefinition Path: " @ %assetDef.getFilename();
}

function AssetBrowser::onSoundAssetEditorDropped(%this, %assetDef, %position)
{
   %targetPosition = EWorldEditor.unproject(%position SPC 1000);
   %camPos = LocalClientConnection.camera.getPosition();
   %rayResult = containerRayCast(%camPos, %targetPosition, -1);
   
   %pos = ObjectCreator.getCreateObjectPosition();

   if(%rayResult != 0)
   {
      %pos = getWords(%rayResult, 1, 3);
   }
   else
   {
      %pos = "0 0 0";  
   }
   
   %assetId = %assetDef.getAssetId();
   
   %newSFXEmitter = new SFXEmitter()
   {
      position = %pos;
      soundAsset = %assetDef.getAssetId();
      pitch = %assetDef.pitchAdjust;
      volume = %assetDef.volumeAdjust;
   };
   
   getScene(0).add(%newSFXEmitter);
   
   EWorldEditor.clearSelection();
   EWorldEditor.selectObject(%newSFXEmitter);
      
   EWorldEditor.isDirty = true;
   
}

function GuiInspectorTypeSoundAssetPtr::onControlDropped( %this, %payload, %position )
{
   Canvas.popDialog(EditorDragAndDropLayer);
   
   // Make sure this is a color swatch drag operation.
   if( !%payload.parentGroup.isInNamespaceHierarchy( "AssetPreviewControlType_AssetDrop" ) )
      return;

   %assetType = %payload.assetType;
   
   if(%assetType $= "SoundAsset")
{
      %module = %payload.moduleName;
      %asset = %payload.assetName;
      
      %targetComponent = %this.targetObject;
      %targetComponent.soundAsset = %module @ ":" @ %asset;
      
      //Inspector.refresh();
   }
   
   EWorldEditor.isDirty = true;
}